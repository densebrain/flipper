import com.android.build.gradle.LibraryExtension

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0-alpha10'
    }
}

plugins {
    id 'de.undercouch.download' version '3.4.3'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'com.jfrog.bintray' version '1.8.4'
    id "com.github.dcendents.android-maven" version '2.1'
}

ext.isSnapshot = { VERSION_NAME.endsWith('-SNAPSHOT') }
ext.isRelease = { ['uploadArchives', 'bintrayUpload'].any { gradle.startParameter.taskNames.contains(it) } }

subprojects {

    apply from: rootProject.file('gradle/buildscript.gradle')

    repositories {
        google()
        mavenLocal()
        mavenCentral()
        jcenter()

        maven { url 'https://jitpack.io' }

        if (isSnapshot()) {
            maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        }
    }

    afterEvaluate {
        Set<String> abiFilters = new HashSet<String>(Arrays.asList((rootProject.properties["abi.variants"] as String).split(",")))
        if (extensions.findByType(LibraryExtension) != null) {
            extensions.configure(LibraryExtension) { LibraryExtension libExt ->
                if (libExt.externalNativeBuild.cmake.path != null)
                    libExt.defaultConfig.externalNativeBuild
                      .cmake
                      .setAbiFilters(abiFilters)
            }
        }
    }
}

ext {
    minSdkVersion = 16
    targetSdkVersion = 28
    compileSdkVersion = 28
    buildToolsVersion = '28.0.3'
    sourceCompatibilityVersion = JavaVersion.VERSION_1_7
    targetCompatibilityVersion = JavaVersion.VERSION_1_7

    versions = [
      androidXAnnotations: "1.1.0-beta01",
      androidXAppcompat      : "1.1.0-alpha04",
      androidXLegacyCoreUI         : "1.0.0",
      androidXRecyclerView   : "1.1.0-alpha04"

    ]

    deps = [
      // Android support
      supportAnnotations : "androidx.annotation:annotation:${versions.androidXAnnotations}",
      supportAppCompat   : "androidx.appcompat:appcompat:${versions.androidXAppcompat}",
      supportCoreUi      : "androidx.legacy:legacy-support-core-ui:${versions.androidXLegacyCoreUI}",
      supportRecyclerView: "androidx.recyclerview:recyclerview:${versions.androidXRecyclerView}",
      supportConstraintLayout: "androidx.constraintlayout:constraintlayout:1.1.3",
      supportEspresso    : 'androidx.test.espresso:espresso-core:3.1.0',
      supportDesign      : "com.google.android.material:material:1.1.0-alpha05",
      supportEspressoIntents : 'androidx.test.espresso:espresso-intents:3.1.0',
      supportTestRunner  : 'androidx.test:runner:1.1.0',
      // Arch
      archPaging         : 'android.arch.paging:runtime:1.0.0',
      // First-party
      soloader           : 'com.facebook.soloader:soloader:0.6.0',
      screenshot         : 'com.facebook.testing.screenshot:core:0.5.0',
      // Annotations
      jsr305             : 'com.google.code.findbugs:jsr305:3.0.1',
      inferAnnotations   : 'com.facebook.infer.annotation:infer-annotation:0.11.2',
      // Litho
      lithoAnnotations   : "com.facebook.litho:litho-annotations:$LITHO_VERSION",
      lithoCore          : "com.facebook.litho:litho-core:$LITHO_VERSION",
      lithoSectionsDebug : "com.facebook.litho:litho-sections-debug:$LITHO_VERSION",
      lithoSectionsCore  : "com.facebook.litho:litho-sections-core:$LITHO_VERSION",
      lithoWidget        : "com.facebook.litho:litho-widget:$LITHO_VERSION",
      lithoProcessor     : "com.facebook.litho:litho-processor:$LITHO_VERSION",
      lithoFresco        : "com.facebook.litho:litho-fresco:$LITHO_VERSION",
      lithoTesting       : "com.facebook.litho:litho-testing:$LITHO_VERSION",
      // Debugging and testing
      guava              : 'com.google.guava:guava:20.0',
      robolectric        : 'org.robolectric:robolectric:3.3',
      junit              : 'junit:junit:4.12',
      hamcrest           : 'org.hamcrest:hamcrest-library:1.3',
      mockito            : 'org.mockito:mockito-core:1.9.5',
      stetho             : 'com.facebook.stetho:stetho:1.5.0',
      okhttp3            : 'com.squareup.okhttp3:okhttp:3.10.0',
      leakcanary         : 'com.squareup.leakcanary:leakcanary-android:1.6.1',
      testCore        : 'androidx.test:core:1.0.0',
      testRules       : 'androidx.test:rules:1.1.0',
      // Plugin dependencies
      rhino              : 'org.mozilla:rhino:1.7.10',
      frescoFlipper      : 'com.facebook.fresco:flipper:1.13.0',
      frescoStetho       : 'com.facebook.fresco:stetho:1.13.0',
      fresco             : 'com.facebook.fresco:fresco:1.13.0',
      mdns               : 'com.github.cicdevelopmentnz:Android-MDNS:v0.0.2'
    ]
}



