
/*
 * Copyright 2014-present Facebook, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Upload to Bintray


def getMavenCentralUsername() {
    return project.hasProperty('mavenCentralUsername') ? property('mavenCentralUsername') : System.getenv('MAVEN_CENTRAL_USERNAME')
}

def getMavenCentralPassword() {
    return project.hasProperty('mavenCentralPassword') ? property('mavenCentralPassword') : System.getenv('MAVEN_CENTRAL_PASSWORD')
}

def shouldSyncWithMavenCentral() {
    return project.hasProperty('syncWithMavenCentral') ? property('syncWithMavenCentral').toBoolean() : false
}

def dryRunOnly() {
    return project.hasProperty('dryRun') ? property('dryRun').toBoolean() : false
}

if (bintrayUsername != null && bintrayApiKey != null) {
    bintray {
        user = bintrayUsername
        key = bintrayApiKey
        configurations = ['archives']
        pkg {
            repo = bintrayRepo
            userOrg = bintrayUserOrg
            name = bintrayName
            desc = bintrayDescription
            websiteUrl = projectUrl
            issueTrackerUrl = issuesUrl
            vcsUrl = scmUrl
            licenses = [POM_LICENSE_NAME]
            dryRun = dryRunOnly()
            override = true
            publish = true
            publicDownloadNumbers = true
            version {
                desc = bintrayDescription

                if (bintrayGpgPassword != null) {
                    gpg {
                        sign = true
                        passphrase = bintrayGpgPassword
                    }
                }

                if (shouldSyncWithMavenCentral()) {
                    mavenCentralSync {
                        sync = true
                        user = getMavenCentralUsername()
                        password = getMavenCentralPassword()
                        close = '1' // If set to 0, you have to manually click release
                    }
                }
            }
        }
    }
}